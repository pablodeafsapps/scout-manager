apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion compilesdk_version
    defaultConfig {
        minSdkVersion minsdk_version
        targetSdkVersion targetsdk_version
        buildToolsVersion buildtools_version
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
        }
        pre {
            dimension "version"
        }
        pro {
            dimension "version"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name == 'release' && (names.contains('dev') || names.contains('pre'))) {
            setIgnore(true)
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    lintOptions {
        abortOnError false
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "./docs/dokka"
    skipEmptyPackages = true   // skip empty packages
    skipDeprecated = false   // skip deprecated
    noStdlibLink = false   // skip documentation related to kotlin-stdlib
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "androidx.appcompat:appcompat:$androidxappcompat_version"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidxconstraintlayout_version"
    implementation "androidx.recyclerview:recyclerview:$androidxrecyclerview_version"
    implementation "androidx.cardview:cardview:$androidxcardview_version"
    implementation "com.google.android.material:material:$material_version"
    // other modules
    implementation project(':domain-layer')
    // 3rd party libraries
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"
    // testing dependencies - Instrumentation Test
    androidTestImplementation "org.mockito:mockito-android:$mockitoandroid_version"
    androidTestImplementation "androidx.test:runner:$androidxtestrunner_version"
    androidTestImplementation "androidx.test:rules:$androidxtestrules_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    // koin testing tools
    androidTestImplementation("org.koin:koin-test:$koin_version") {
        exclude group: 'org.mockito'
        exclude group: 'mockito-inline'
    }
}